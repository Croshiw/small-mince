"
Code inspired from: ClyJumpToTestClassCommand and ClyJumpToTestMethodCommand
"
Class {
	#name : #SMTestClassBuilder,
	#superclass : #Object,
	#traits : 'SMInternalClassesTrait',
	#classTraits : 'SMInternalClassesTrait classTrait',
	#instVars : [
		'theClass',
		'theMethods',
		'resultClass'
	],
	#category : #'SmallMince-Core-4.Synthesis'
}

{ #category : #adding }
SMTestClassBuilder >> addCommentForClass [
	resultClass comment: 'This Class is generated by TestGrinder, from ',theClass
]

{ #category : #'as yet unclassified' }
SMTestClassBuilder >> buildClass [
	| className |
	className := self testClassName.
	resultClass := TestCase
		subclass: className
		instanceVariableNames: ''
		classVariableNames: ''
		package: self defaultTempPackage.
	self addCommentForClass
]

{ #category : #'as yet unclassified' }
SMTestClassBuilder >> buildFor: aClass methods: listMethods [
	theClass := aClass.
	theMethods := listMethods.
	
	self removeIfExists.
	self buildClass.
	self installMethods
]

{ #category : #'as yet unclassified' }
SMTestClassBuilder >> defaultTempPackage [
	^ 'SmalltalkTestGrinderTempClasses'
]

{ #category : #'as yet unclassified' }
SMTestClassBuilder >> installMethods [
	theMethods do: [ :m | resultClass  compile: m formattedCode  classified: 'test' ]
	
]

{ #category : #'as yet unclassified' }
SMTestClassBuilder >> removeIfExists [
	(self defaultTempPackage asPackageIfAbsent: [ ^ true ]) classes
		select: [ :item | item name = self testClassName ]
		thenDo: [ :item | item removeFromSystem ]
]

{ #category : #'as yet unclassified' }
SMTestClassBuilder >> testClassName [
	^ (theClass , 'GenTest') asSymbol
]
